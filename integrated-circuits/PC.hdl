// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Mux16(a=in[0..15],b=false,sel=reset,out=inorzeropath);
    Or(a=reset,b=load,out=resetorload);
    Mux16(a=regorinc,b=inorzeropath,sel=resetorload,out=reginput);
    Or(a=load,b=inc,out=loadorinc);
    Or(a=reset,b=loadorinc,out=loadorincorreset);
    Register(in=reginput,load=loadorincorreset,out=regoutput,out=out[0..15]);
    Inc16(in=regoutput,out=incoutput);
    Mux16(a=regoutput,b=incoutput,sel=inc,out=regorinc);
}
